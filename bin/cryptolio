#! /usr/bin/perl

use strict;
use warnings;

our $VERSION = "0.03";

use LWP::UserAgent;
use JSON;
use List::Util qw(sum uniq);
use Text::Table;
use File::Slurp;
use File::Basename;

my $url_listings   = "https://api.coinmarketcap.com/v2/listings";
my $url_ticker     = "https://api.coinmarketcap.com/v2/ticker/%s/?convert=EUR";
my $url_global     = "https://api.coinmarketcap.com/v2/global/?convert=EUR";
my $portfolio_path = glob('~/.cryptolio.json');

sub search_listings_by_field {
    my ($listings, $field, $query) = @_;
    return grep { lc $listings->{$_}->{$field} eq lc $query } keys %$listings;
}

sub stringify_list_of_cmc_ids {
    my ($listings, @ids) = @_;
    my @website_slugs = map $listings->{$_}{website_slug}, @ids;
    return join(', ', sort {$a cmp $b} @website_slugs);
}

my %portfolio;
eval {
    %portfolio = %{ from_json(read_file($portfolio_path)) };
};
if ($@) {
    my $error = $@;
    if (-e $portfolio_path) {
        print "Couldn't process portfolio file. Error: $error";
        exit;
    }
    write_file($portfolio_path, '{}');
}

my $ua = LWP::UserAgent->new(
    agent => "Cryptolio v$VERSION",
);

my $global_data = from_json($ua->get($url_global)->decoded_content);

if (@ARGV > 1) {
    my $currency = shift;
    my $new_position = shift;
    my $exchange = shift || "wallet";
    my $listings = from_json($ua->get($url_listings)->decoded_content);
    $listings = { map { $_->{id} => $_ } @{$listings->{data}} };

    my @search_order = ("name", "website_slug", "symbol", "id");

    my @coin_id;
    my @non_unique_matches;
    while (@coin_id != 1 and @search_order) {
        my $search_field = shift @search_order;
        @coin_id = search_listings_by_field($listings, $search_field, $currency);
        @non_unique_matches = @coin_id if (@coin_id > 1);
    }
    if (@coin_id != 1) {
        if (@non_unique_matches) {
            my $example_name = stringify_list_of_cmc_ids($listings, $non_unique_matches[0]);
            print "There is no such unique currency. Please specify name (example: cryptolio $example_name $new_position). Matches: "
                  . stringify_list_of_cmc_ids($listings, @non_unique_matches) . "\n";
        } else {
            print "Your currency is not available on coinmarketcap. Available currencies are: "
                  . stringify_list_of_cmc_ids($listings, keys %$listings) . "\n";
        }
        exit;
    }

    if ($new_position == 0) {
        delete $portfolio{$coin_id[0]};
    }
    else {
        $portfolio{$coin_id[0]} = {
            q => $new_position,
            exchange => $exchange,
        };
    }
    write_file($portfolio_path, to_json(\%portfolio));
    exit;
}

my $currency = uc( shift || 'USD' );
my $order = shift || 'value';

my $total_market_cap = $global_data->{data}{quotes}{$currency}{"total_market_cap"};
%portfolio = map {
    $_ => {
        %{$portfolio{$_}},
        market_data => from_json($ua->get(sprintf($url_ticker, $_))->decoded_content)->{"data"},
    }
} keys %portfolio;

my @portfolio_full = map {
    my $portfolio_data = $portfolio{$_};
    my $market_data = $portfolio_data->{market_data};
    {
        currency     => $_,
        ticker       => $market_data->{symbol},
        qty          => $portfolio_data->{q},
        exchange     => $portfolio_data->{exchange},
        price        => $market_data->{quotes}{$currency}{price},
        value        => $market_data->{quotes}{$currency}{price} * $portfolio_data->{q},
        market_share => $market_data->{quotes}{$currency}{market_cap}/$total_market_cap*100,
        market_cap   => $market_data->{quotes}{$currency}{market_cap}/1000_000,
        change_1h    => $market_data->{quotes}{$currency}{percent_change_1h},
        change_24h   => $market_data->{quotes}{$currency}{percent_change_24h},
        change_7d    => $market_data->{quotes}{$currency}{percent_change_7d},
    };
} keys %portfolio;

my $portfolio_value = sum map $_->{value}, @portfolio_full;
my $portfolio_value_1h  = sum map $_->{value}/(1+$_->{change_1h}/100), @portfolio_full;
my $portfolio_value_24h = sum map $_->{value}/(1+$_->{change_24h}/100), @portfolio_full;
my $portfolio_value_7d  = sum map $_->{value}/(1+$_->{change_7d}/100), @portfolio_full;
my $market_share_represented_in_portfolio = sum map $_->{market_share}, @portfolio_full;
my $market_cap_represented_in_portfolio = sum map $_->{market_cap}, @portfolio_full;
my @exchanges = uniq grep $_ ne 'wallet', map $_->{exchange}, @portfolio_full;
my %counterparty_risks = map {
    my $e = $_;
    my $total_value_on_exchange = sum map $_->{value}, grep {$_->{exchange} eq $e} @portfolio_full;
    ($e => $total_value_on_exchange);
} @exchanges;

if(!$portfolio_value) {
    my $program_name = basename $0;
    print "Portfolio is empty. Add some cryptocurrencies first. Example:\n\n"
    ."$program_name BTC 1 bitfinex # this would add 1 btc to the portfolio on the "
    ."bitfinex exchange (the third parameter [exchange] is optional)\n\n";
    exit;
}

@portfolio_full = map { {%$_, share => $_->{value}/$portfolio_value} } @portfolio_full;
@portfolio_full = sort {$b->{$order} <=> $a->{$order} || $b->{market_cap} <=> $a->{market_cap}} @portfolio_full;

my $table = Text::Table->new(
    "currency",
    {title => "position", align => "left"},
    {title => "price", align => "left"},
    {title => "value",align => "left"},
    {title => "share", align => "left"},
    "market share",
    {title => "market cap", align => "left"},
    "change 1h",
    "change 24h",
    "change 7d",
    "exchange",
);

my $portfolio_change_1h  = 100*($portfolio_value/$portfolio_value_1h-1);
my $portfolio_change_24h = 100*($portfolio_value/$portfolio_value_24h-1);
my $portfolio_change_7d  = 100*($portfolio_value/$portfolio_value_7d-1);

my @table_contents = map [
    $_->{ticker},
    $_->{qty},
    sprintf("%.2f",  $_->{price}),
    sprintf("%.2f",  $_->{value}),
    sprintf("%.2f%%", $_->{share}*100),
    sprintf("%.2f%%", $_->{market_share}),
    sprintf("%.0fM", $_->{market_cap}),
    ($_->{change_1h} > 0 ? "\e[32m" : "\e[31m") . sprintf("%.2f%%", $_->{change_1h}),
    ($_->{change_24h} > 0 ? "\e[32m" : "\e[31m") . sprintf("%.2f%%", $_->{change_24h}),
    ($_->{change_7d} > 0 ? "\e[32m" : "\e[31m") . sprintf("%.2f%%\e[0m", $_->{change_7d}),
    $_->{exchange},
], @portfolio_full;
push @table_contents, [
    "TOTAL",
    "---",
    "---",
    sprintf("%.2f", $portfolio_value),
    "100%",
    sprintf("%.1f%%", $market_share_represented_in_portfolio),
    sprintf("%.0fM", $market_cap_represented_in_portfolio),
    ($portfolio_change_1h > 0 ? "\e[32m" : "\e[31m") . sprintf("%.2f%%", $portfolio_change_1h),
    ($portfolio_change_24h > 0 ? "\e[32m" : "\e[31m") .sprintf("%.2f%%", $portfolio_change_24h),
    ($portfolio_change_7d > 0 ? "\e[32m" : "\e[31m") . sprintf("%.2f%%\e[0m", $portfolio_change_7d),
    "---",
];
$table->load(@table_contents);

print $table;

print "\nTotal cryptocurrency market cap: " . sprintf("%.2fB", $total_market_cap/1000_000_000) . "\n";
print "\nCounterparty risks:\n\n";

my $counterparty_risk_table = Text::Table->new(
    "exchange   ",
    {title => "risk", align => "left"},
);
@table_contents = map [$_ => sprintf("%.2f", $counterparty_risks{$_})],
                  sort {$counterparty_risks{$b} <=> $counterparty_risks{$a}}
                  keys %counterparty_risks;
$counterparty_risk_table->load(@table_contents);

print $counterparty_risk_table;
